/** 
 * File:	reipl_bootloader_finish.ycp
 * 
 * Module:	reIPL
 * 
 * Authors:	Ulrich Hecht <uli@suse.de>
 *		Lukas Ocilka <locilka@suse.cz>
 * 
 * $Id:$ 
 * 
 */
{
    // YaST finish client returns (map) $[
    //     "different" : (boolean) whether changed,
    //     "ipl_msg"   : (string) localized message,
    // ]

    import "Arch";
    import "Reipl";

    textdomain "reipl";

    boolean different = false;
    string ipl_msg = "";

    // Other architectures do not support it
    if (Arch::s390 ()) {
	map <string, any> oldConfiguration = Reipl::ReadState ();
	map <string, any> newConfiguration = oldConfiguration;

	if (oldConfiguration != nil) {
	    Reipl::ModifyReiplWithBootPartition (newConfiguration);

	    map oldCcwMap = (map)oldConfiguration["ccw"]:nil;
	    map newCcwMap = (map)newConfiguration["ccw"]:nil;
	    map oldFcpMap = (map)oldConfiguration["fcp"]:nil;
	    map newFcpMap = (map)newConfiguration["fcp"]:nil;

	    different =  oldConfiguration["method"]:"a" != newConfiguration["method"]:"b"
			    && oldCcwMap["device"]:"a" != newCcwMap["device"]:"b"
			    && oldCcwMap["loadparm"]:"a" != newCcwMap["loadparm"]:"b"
			    && oldFcpMap["device"]:"a" != newFcpMap["device"]:"b"
			    && oldFcpMap["wwpn"]:"a" != newFcpMap["wwpn"]:"b"
			    && oldFcpMap["lun"]:"a" != newFcpMap["lun"]:"b"
			    && oldFcpMap["bootprog"]:"a" != newFcpMap["bootprog"]:"b"
			    && oldFcpMap["br_lba"]:"a" != newFcpMap["br_lba"]:"b";

	    if (newConfiguration["method"]:"ERROR" == "ccw") {
		string dev = substring (newCcwMap["device"]:"", 4, 4);

		// TRANSLATORS: part of a shutdown message
		// %1 is replaced with a device name
		// Newline at the end is required
		ipl_msg = sformat (_("After shutdown, reload the system
with an IPL from DASD '%1'.
"), dev);

	    } else if (newConfiguration["method"]:"ERROR" == "fcp") {
		string dev  = substring (newFcpMap["device"]:"",  4, 4);
		string wwpn = newFcpMap["wwpn"]:"";
		string lun  = newFcpMap["lun"]:"";

		// TRANSLATORS: part of a shutdown message
		// %1 is replaced with a FCP name
		// %2 is replaced with a WWPN name
		// %3 is replaced with a LUN name
		// Newline at the end is required
		ipl_msg = sformat (_("After shutdown, reload the system
with an IPL from FCP '%1' with WWPN '%2'
and LUN '%3'.
"), dev, wwpn, lun);
	    }
	}
    }

    return $["different":different, "ipl_msg":ipl_msg];
}